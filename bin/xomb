#!perl
#
# xomb - launcher for Game::Xomb

use 5.24.0;
use warnings;
use Getopt::Long qw(GetOptions);
use Game::Xomb;

our ($ddelay, $err, $seed);

GetOptions(
    'help|h|?' => sub {
        warn <<'TLDR';
Usage: xomb [options]

  --err     - send STDERR to this file if not already redirected
  --ddelay  - draw delay in seconds between moves
  --seed    - set the game seed
  --version - shows xomb version and then quits

xomb hides STDERR by default to not show warning or error messages;
debug with a command similar to

  xomb 2>log
  xomb --err=log

and then inspect the log file when things do go awry

TLDR
        exit 64;
    },
    'err=s'     => \$err,
    'ddelay=f'  => \$ddelay,
    'seed=i'    => \$seed,
    'version|v' => sub { say $Game::Xomb::VERSION; exit 1 },
) or exit 64;

$ddelay ||= 0.01;

if (-t STDERR) {
    close STDERR;
    $err //= "$ENV{HOME}/co/Game-Xomb/log";    # DBG
    if (defined $err) {
        unless (open STDERR, '>', $err) {
            say "Could not redirect STDERR to '$err': $!";
            exit 1;
        }
        STDERR->autoflush(1);
    }
}

srand $seed if defined $seed;
Game::Xomb::game_loop($ddelay);
exit(1);                                       # NOTREACHED

__END__
=encoding utf8

=head1 NAME

B<xomb> - a game featuring @ versus the Xarci Bedo

=head1 SYNOPSIS

B<xomb> I<--err=file> I<--ddelay=f> I<--seed=i> I<--version>

=head1 DESCRIPTION

B<xomb> is a computer fantasy game. The object of the game is to survive
the attacks of the fiendish Xarci Bedo and obtain and escape with the
fabled Dragonstone. Which, despite the name, is actually a vegetable.

=head2 Options

=over 4

=item --err=file

Send STDERR to this file if not already redirected.

=item --ddelay=f

Delay between redraw of from/to motions. Floating point value ideally
well below 1.0.

=item --seed=i

Start the game with the seed set to a particular integer value.

=item --version

Shows the B<xomb> version and quits the game.

=back

=head2 An Overview of Minos III 

The most recent message is shown at the top of the screen. More of these
can be viewed using the C<M> command. A status bar at the bottom shows
the level, the energy cost of the last engery-consuming move, your
shield points (that ideally should be kept above zero), the item and
ground tile (if any) of the cell you are in, and an error code display
slot. L</"DIAGNOSTICS"> has details on the error codes. The middle of
the screen shows the level map. Various L</"Symbols"> occupy this space.

=head2 Commands

The C<?> key in-game will show a list of commands available.

=head2 Symbols

These may be inspected in-game using the examine command. At most
only one item and one Xarci Bedo and one ground tile can occupy the
same cell.

=over 4

=item C<@>

Your location on the level map.

=item C<%>

A gate (stair) to the next level. A gate takes some amount of time to
activate. You can also fall through holes; this is quicker but causes
some amount of damage. Ascent is only possible with the Dragonstone.

=item C<*>

A gemstone. Collect these.

=item C<#>

Wall. Generally impassable.

=item C<.>

An empty cell.

=item C< >

Holes are represented by a black space and will drop you to the
next level.

=back

The Xarci Bedo are represented by uppercase ASCII letters.

=head1 DIAGNOSTICS

Various messages may be shown by the Patrol Kombat Computer (PKC) in
response to command inputs. Details on important codes are listed below.
Consult the Green Operations Manual for complete scenario process
control instructions.

=head2 PKC-0001

A move was attempted beyond the boundaries of the game. In strict mode
this would normally result in the immediate termination of the
contestant.

=head2 PKC-0002

The motion control guidance system has encountered a significant
obstacle and signals that fact with this code.

=head2 PKC-0004

Gate activation error.

=head2 PKC-0010

Dragonstone is required for vertical ascent module gate activation.

=head2 PKC-007E

Environmental hazard proximity alarm.

=head2 PKC-0099

Gyroscopic control system reports unexpected acceleration.

=head2 PKC-0101

Item retrieval command call failure.

=head2 PKC-0102

Inventory stack allocation failure.

=head2 PKC-0104

Item disposal process error. Clear item exit disposal slot before retry.

=head2 PKC-0112

Underflow on inventory stack query request.

=head2 PKC-0302

Target acquisition lock failure.

=head2 PKC-1203

Spurious interrupt on data bus array.

=head2 PKC-1202

Executive overflow on guidance module.

=head2 PKC-1220

Background subspace comm link protocol error.

=head1 EXIT STATUS

B<xomb> exits with a 0 on victory, and >0 in every other case.

=head1 SEE ALSO

L<Game::Xomb>, L<rogue(6)>

Operations Manual (Green)

=head1 AUTHOR

Jeremy Mates

=head1 BUGS

B<xomb> assumes that the terminal used has a black background. B<xomb>
assumes that the terminal supports various ANSI or XTerm Control
Sequences.

=cut
