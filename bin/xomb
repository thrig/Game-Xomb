#!perl
#
# xomb - launcher for Game::Xomb

use 5.24.0;
use warnings;
use Getopt::Long qw(GetOptions);
use Game::Xomb;

GetOptions(
    'help|h|?' => sub {
        warn <<'TLDR';
Usage: xomb [options]

  --err     - send STDERR to this file if not already redirected
  --seed    - set the game seed
  --version - shows xomb version and then quits

xomb hides STDERR by default to not show warning or error messages;
debug with a command similar to

  xomb 2>log
  xomb --err=log

and then inspect the log file when things do go awry

TLDR
        exit 64;
    },
    'err=s'     => \my $err,
    'seed=i'    => \my $seed,
    'version|v' => sub { say $Game::Xomb::VERSION; exit 1 },
) or exit 64;

if (-t STDERR) {
    close STDERR;
    $err //= "$ENV{HOME}/co/Game-Xomb/log";    # DBG
    if (defined $err) {
        unless (open STDERR, '>', $err) {
            say "Could not redirect STDERR to '$err': $!";
            exit 1;
        }
        STDERR->autoflush(1);
    }
}

srand $seed if defined $seed;
Game::Xomb::game_loop;
exit(1);                                       # NOTREACHED

__END__
=encoding utf8

=head1 NAME

B<xomb> - a game featuring @ versus the Xarci Bedo

=head1 SYNOPSIS

B<xomb> I<--err=file> I<--seed=i> I<--version>

=head1 DESCRIPTION

B<xomb> is a computer fantasy game. The object of the game is to survive
the attacks of the fiendish Xarci Bedo and obtain and escape with the
fabled Dragonstone. Which, despite the name, is actually a vegetable.

=head2 Options

=over 4

=item --err=file

Send STDERR to this file if not already redirected.

=item --seed=i

Start the game with the seed set to a particular integer value.

=item --version

Shows the B<xomb> version and quits the game.

=back

=head2 An Overview of Minos III 

The most recent message is shown at the top of the screen. More of these
can be viewed using the C<M> command. A status bar at the bottom shows
the level, the energy cost of the last engery-consuming move, the shield
points, the item and ground tile (if any) of the current cell, and an
error code display slot. Consult the L</"DIAGNOSTICS"> section for
details on these.

The middle of the screen shows the level map. Various L</"Symbols"> are
present in this space. Note that the Field Operation View (FOV) readouts
may be impacted by the harsh conditions on Minos III. Rubble in
particular may cause the suboptimal degredation of array view scans.

=head2 Commands

     y  k  u     Motion is traditional to rogue(6) as shown in the
      \ | /      compass to the left. Other commands, of which some
    h - @ - l    take time to complete, include:
      / | \             
     b  j  n                . - wait a turn      x - examine board
                          g , - pick up item     i - show inventory
    M - show messages     < > - activate gate    e - equip a gem
    E - clear PKC code    C-l - redraw screen    R - remove a gem
    ? - show help         v   - show version     d - drop a gem

    Esc or q will exit from sub-displays such as this one. Prompts
    must be answered with Y to carry out the action; N or n or Esc
    will reject the action.

With numlock enabled a numeric keypad may be able to be used to
navigate.

=head2 Symbols

These may be inspected in-game using the examine command, assuming they
were picked up by the FOV scan. At most only one Xarci Bedo, item, and
ground tile can occupy the same cell. Scientists believe this is due to
particularities of the geology on Minos III.

=over 4

=item C<@>

Vessel location.

=item C<%>

A gate to the next level via the C<< > >> command. A gate takes some
time to complete the level transition. Ascent is only possible with the
Dragonstone using C<< < >> on the gate.

=item C<*>

A gemstone. These may be equipped from the inventory to provide shield
repair over time, though this will damage the gem due to harmonic
stresses. The gem value is shown before the name:

    B) * (88) Bloodstone
    C) * (19) Moonstone
    D) * (58) Warped Moonstone
    -- press Esc to continue or (d)rop, (e)quip --

=item C<#>

Wall. Impassable. Generally blocks FOV.

=item C<^>

Rubble. Somewhat passable. Often blocks FOV.

=item C<~>

Acid pond.

=item C<.>

An empty cell.

=item C<o>

A hole that will drop the vessel to the next level. Pilots should be
aware that safe descent is not certified under all conditions.

=back

The Xarci Bedo are represented by uppercase ASCII letters, and exhibit
the usual fantasy tropes: Gatling Autocannon, Railgun, etc.

=head1 DIAGNOSTICS

Various messages may be shown by the Patrol Kombat Computer (PKC) in
response to command inputs. Details on important codes are listed below.
Consult the VP XII Operations Manual (Green) for complete scenario
process control instructions.

=head2 PKC-0001

A move was attempted beyond the boundaries of the game. In strict mode
this would normally result in the immediate termination of the
contestant.

=head2 PKC-0002

The motion control guidance system has encountered a significant
obstacle and signals that fact with this code.

=head2 PKC-0004

Gate activation error.

=head2 PKC-0010

Dragonstone is required for vertical ascent module gate activation.

=head2 PKC-007E

Environmental hazard proximity alarm.

=head2 PKC-0099

Gyroscopic alignment control array reports unexpected acceleration.

=head2 PKC-0101

Item retrieval command call failure.

=head2 PKC-0102

Inventory stack allocation failure.

=head2 PKC-0104

Item disposal process error. Clear item exit disposal slot before retry.

=head2 PKC-0111

Incompatible item use handler exception error.

=head2 PKC-0112

Underflow on inventory stack query request.

=head2 PKC-0113

Underflow on shield repair module feed unit.

=head2 PKC-0302

Target acquisition lock failure.

=head2 PKC-1203

Spurious interrupt on data bus array.

=head2 PKC-1202

Executive overflow on guidance module.

=head2 PKC-1220

Background subspace comm link protocol query.

=head1 EXIT STATUS

B<xomb> exits with a C<0> on victory, and C<< >0 >> in every other case.

=head1 SEE ALSO

L<Game::Xomb>, L<rogue(6)>

VP XII Operations Manual (Green)

=head1 AUTHOR

Jeremy Mates

=head1 BUGS

B<xomb> assumes that the terminal supports various ANSI or XTerm Control
Sequences. Given certain time constraints (7DRL 2020) not much
portability testing has been done.

=cut
